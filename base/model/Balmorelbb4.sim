* File BB4.sim to the Balmorel model.
* First version 2009(hr). Modified spring 2015 (wx, hr). Present version 20151217(hr).

* This file contains simulation structure for model BALBASE4.
* BALBASE4: optimisation with endogenous investments simultaneuosly for all (S,T) over all Y (or possibly subsets of Y).

* The model is still under developemnt, and the present version is not public.
* The final model will be available from www.balmorel.com under open source conditions.

* Note: although not necessary, there are a number of "$ifi %BB4%==yes" conditions,
* this is to make some of the differences relative to bb123.sim more visible.

*-------------------------------------------------------------------------------
* INITIALISATIONS BEFORE LOOPING OVER $YALIAS%:
*-------------------------------------------------------------------------------

IS3(S)   = YES;



* Initialize the first year in the simulation:


VGKN.L(Y,IA,G)        = 0;
VXKN.l(Y,IR,IRI)      = 0;
VDECOM.l(Y,IA,G)      = 0;



*$IF((%investmentfromyear%== yes),  TODO WHEN DECOMMISIONING IN EXISTING TECHNOLOGIES IS INTRODUCED
*
*         VDECOM.UP(Y,IA,G)$( YVALUE(Y) le (GINVESTSTART-1) )=EPS;
* ;



* Unload IWEIGHTY for the output tool
execute_unload '%relpathModel%../output/temp/IWEIGHTY.gdx' IWEIGHTY;

*IFIRSTYEARNEXTBB4 = SUM(Y$(ORD(Y) EQ 1), YVALUE(Y)); !! todo not in use
IY411PREVY(Y,Y-1)= YES;


*------------ Calculation of IYWEIGHT_ANNUITYCG and IYWEIGHT_ANNUITYCX to include/exclude the payment of the annuities---------------------

**GENERATION

LOOP(C,
  LOOP(IGKFIND$SUM(IA$ICA(C,IA), AGKN(IA,IGKFIND)),
    LOOP(IYALIAS$((IYALIAS.VAL LE GDATA(IGKFIND,'GDLASTYEAR'))$GDATA(IGKFIND,'GDLASTYEAR') AND (IYALIAS.VAL GE GDATA(IGKFIND,'GDFROMYEAR'))),
      LOOP(Y$(ORD(Y) EQ ORD(IYALIAS)),
        IINVYEARG_LEFT(C,IYALIAS,Y,IGKFIND)=GDATA(IGKFIND,'GDLIFETIME');
      );
    );
  );
);

LOOP(C,
  LOOP(IGKFIND$SUM(IA$ICA(C,IA), AGKN(IA,IGKFIND)),
    LOOP(IYALIAS$((IYALIAS.VAL LE GDATA(IGKFIND,'GDLASTYEAR'))$GDATA(IGKFIND,'GDLASTYEAR') AND (IYALIAS.VAL GE GDATA(IGKFIND,'GDFROMYEAR'))),
      ILOOPBREAK=0;
      IN_INV_G(C,IYALIAS,IGKFIND)=ROUND(IWEIGHTY(IYALIAS)/GDATA(IGKFIND,'GDLIFETIME'));
*Correction of IN_INV_G
        IF (IN_INV_G(C,IYALIAS,IGKFIND) EQ 0,
          IN_INV_G(C,IYALIAS,IGKFIND)=1;
        );
        LOOP(Y$(ORD(Y) GE ORD(IYALIAS) AND (ILOOPBREAK EQ 0)),
*First year's annuity
          IF (ORD(Y) EQ ORD(IYALIAS),
            IYWEIGHT_ANNUITYCG(IYALIAS,Y,IGKFIND)= 1;
            IF(IINVYEARG_LEFT(C,IYALIAS,Y,IGKFIND)*IN_INV_G(C,IYALIAS,IGKFIND) LE IWEIGHTY(Y),
              ILOOPBREAK=1;
            );
            IINVYEARG_LEFT(C,IYALIAS,Y+1,IGKFIND)$(ORD(Y) NE CARD(IYALIAS2))=GDATA(IGKFIND,'GDLIFETIME')*IN_INV_G(C,IYALIAS,IGKFIND)-IWEIGHTY(Y);
          );
*Remaining year's annuity
        IF (ORD(Y) GT ORD(IYALIAS),
          IF ( IINVYEARG_LEFT(C,IYALIAS,Y,IGKFIND) GT IWEIGHTY(Y) ,
            IYWEIGHT_ANNUITYCG(IYALIAS,Y,IGKFIND)= 1;
              IINVYEARG_LEFT(C,IYALIAS,Y+1,IGKFIND)$(ORD(Y) NE CARD(IYALIAS2))=IINVYEARG_LEFT(C,IYALIAS,Y,IGKFIND)-IWEIGHTY(Y);
            );
            IF ((IINVYEARG_LEFT(C,IYALIAS,Y,IGKFIND) GE IWEIGHTY(IYALIAS)/2) AND (IINVYEARG_LEFT(C,IYALIAS,Y,IGKFIND) LE IWEIGHTY(Y)) ,  !!Assumption: if the reamining years are more ore equal than half of IWEIGHTY, then the technology is in in this snapshot
              IYWEIGHT_ANNUITYCG(IYALIAS,Y,IGKFIND)= 1;
              ILOOPBREAK=1;
            );
            IF ((IINVYEARG_LEFT(C,IYALIAS,Y,IGKFIND) LT IWEIGHTY(IYALIAS)/2) AND (IINVYEARG_LEFT(C,IYALIAS,Y,IGKFIND) LE IWEIGHTY(Y)) , !!Assumption: if the reamining years are less than half of IWEIGHTY, then the technology is out in this snapshot
              IYWEIGHT_ANNUITYCG(IYALIAS,Y,IGKFIND)= 0;
              ILOOPBREAK=1;
            );
          );
        );
    );
  );
);

** TRANSMISSION LINES

LOOP(C,
  LOOP(IYALIAS,
    LOOP(Y$(ORD(Y) EQ ORD(IYALIAS)),
      IINVYEARX_LEFT(C,IYALIAS,Y)=LIFETIME_X;
    );
  );
);


LOOP(C,
  LOOP(IYALIAS,
    ILOOPBREAK=0;
    IN_INV_X(C,IYALIAS)=ROUND(IWEIGHTY(IYALIAS)/LIFETIME_X);
*Correction of inv
    IF ( IN_INV_X(C,IYALIAS) EQ 0,
      IN_INV_X(C,IYALIAS)=1;
    );
    LOOP(Y$(ORD(Y) GE ORD(IYALIAS) AND (ILOOPBREAK EQ 0)),

*First year's annuity
      IF ( ORD(Y) EQ ORD(IYALIAS),
        IYWEIGHT_ANNUITYCX(IYALIAS,Y)= 1;
        IF(IINVYEARX_LEFT(C,IYALIAS,Y)*IN_INV_X(C,IYALIAS) LE IWEIGHTY(Y),
        ILOOPBREAK=1;
        );
        IINVYEARX_LEFT(C,IYALIAS,Y+1)$(ORD(Y) NE CARD(IYALIAS2))=LIFETIME_X*IN_INV_X(C,IYALIAS)-IWEIGHTY(Y);
      );
*Remaining year's annuity
      IF ( ORD(Y) GT ORD(IYALIAS),
        IF ( IINVYEARX_LEFT(C,IYALIAS,Y) GT IWEIGHTY(Y) ,
          IYWEIGHT_ANNUITYCX(IYALIAS,Y)= 1;
          IINVYEARX_LEFT(C,IYALIAS,Y+1)$(ORD(Y) NE CARD(IYALIAS2))=IINVYEARX_LEFT(C,IYALIAS,Y)-IWEIGHTY(Y);
        );
        IF ( (IINVYEARX_LEFT(C,IYALIAS,Y) GE IWEIGHTY(Y)/2) AND (IINVYEARX_LEFT(C,IYALIAS,Y) LE IWEIGHTY(Y)) ,
          IYWEIGHT_ANNUITYCX(IYALIAS,Y)= 1;
          ILOOPBREAK=1;
        );
        IF ( (IINVYEARX_LEFT(C,IYALIAS,Y) LT IWEIGHTY(Y)/2) AND (IINVYEARX_LEFT(C,IYALIAS,Y) LE IWEIGHTY(Y)) ,
          IYWEIGHT_ANNUITYCX(IYALIAS,Y)= 0;
          ILOOPBREAK=1;
        );
      );
    );
  );
);


*------------ END of Calculation of the correction factor for the annuities---------------------

* ----------------- Assign IAGKFX, IAGKN, IAGKNY, IAGK_HASORPOT ----------------------------------

IAGKFX(Y,IA,G)        = YES$GKFX(Y,IA,G);
IAGK_HASORPOT(Y,IA,G) = YES$GKFX(Y,IA,G);

IAGKN(IA,G)=NO;
IAGKNY(Y,IA,G)=NO;

$ifi not %TechInvest%==yes $goto Technology_investments_end

* IAGKN and IAGKNY indicates when and where technologies may be invested, based on various combinations of input data.
* Initially: identical to AGKN:
IAGKN(IA,G) = AGKN(IA,G);

* Note: by definition, there should be no GGG in AGKN(aaa,GGG) with GDATA(GGG,'GDKVARIABL')=1.
* This is to be checked in error2.inc: TODO. For now, overwrite to make sure:  No new capacity if capacity is not specified to be found endogeneously:
IAGKN(IA,IGKKNOWN) = NO;

* Fuel limitations: No new capacity nor new generation if zero fuel potential.
LOOP((FFF,G)$IGF(G,FFF),
  LOOP(C$FKPOT(C,FFF), !! Default convention on FKPOT: 0 implies no restriction, 'EPS' implies zero MW.
    LOOP(IA$ICA(C,IA), IF((FKPOT(C,FFF) EQ 0), IAGKN(IA,G) = NO;); )); !! Because it is EPS
  LOOP(IR$FKPOT(IR,FFF),
    LOOP(IA$RRRAAA(IR,IA), IF((FKPOT(IR,FFF) EQ 0), IAGKN(IA,G) = NO;); )); !! Because it is EPS
  LOOP(IA$FKPOT(IA,FFF),
    IF((FKPOT(IA,FFF) EQ 0), IAGKN(IA,G) = NO;); ); !! Because it is EPS
);

IAGKNY(Y,IA,G) = IAGKN(IA,G);
IAGKNY(Y,IA,G)$(YVALUE(Y) LT GDATA(G,'GDFROMYEAR') OR (YVALUE(Y) GT GDATA(G,'GDLASTYEAR')) AND (GDATA(G,'GDLASTYEAR') GT 0) OR (YVALUE(Y) LT GINVESTSTART))=NO;

*Update of IAGK_HASORPOT based on potential production

IAGK_HASORPOT(Y,IA,G)$IAGKNY(Y,IA,G) = YES;

***This loop makes sure that if you invest in a technology in the year Y, you can use it until it is decommissioned, even if you do not allow for investments on this technology in the future
LOOP(Y$(ORD(Y) NE CARD(Y)),
  LOOP(IA,
    LOOP(G$IAGKNY(Y,IA,G),
      LOOP(IYALIAS2$((ORD(IYALIAS2) GT ORD(Y)) AND ((YVALUE(Y)+GDATA(G,'GDLIFETIME')) GT (YVALUE(IYALIAS2)))),
        IAGK_HASORPOT(IYALIAS2,IA,G)= IAGK_HASORPOT(IYALIAS2,IA,G)+IAGKNY(Y,IA,G);
      );
    );
  );
);

$label  Technology_investments_end

* ----------------- End: Assign IAGKFX, IAGKN, IAGKNY, IAGK_HASORPOT -----------------------------


*------------------ Assign IXK_HASORPOT and IXKN based on exogenously given or potential investment  ----------------------------------

* Transmision investments is subject to control by the option TransInvest,
* therefore set a default and update if investments are intended.

IXK_HASORPOT(Y,IRE,IRI) = YES$XKFX(Y,IRE,IRI);
IXKN(Y,IRE,IRI)=NO;

$ifi not %TransInvest%==yes $goto Transmission_investments_end

IXKN(Y,IRE,IRI) = YES$(XINVCOST(Y,IRE,IRI) AND (XINVCOST(Y,IRE,IRI) LT INF) AND (XINVESTSTART LE YVALUE(Y)));

***This loop makes sure that if you invest in one line in the year Y, you can use this line in the future, even if you do not allow for investments on this line in the future
LOOP(Y$(ORD(Y) GT 1),
  LOOP(IRE,
    LOOP(IRI,
      IF ((XINVCOST(Y-1,IRE,IRI) AND (XINVCOST(Y-1,IRE,IRI) LT INF) AND (XINVESTSTART LE YVALUE(Y))),
        IXK_HASORPOT(Y,IRE,IRI)=IXKN(Y,IRE,IRI)+YES$XKFX(Y,IRE,IRI);
      );
      LOOP(IYALIAS2$((ORD(IYALIAS2) GE ORD(Y)) AND (ORD(IYALIAS2) NE CARD(IYALIAS2))),
      IXK_HASORPOT(Y+1,IRE,IRI)= IXK_HASORPOT(Y+1,IRE,IRI)+IXKN(Y,IRE,IRI);
      );
    );
  );
);

$label  Transmission_investments_end

*------------------ End: Assign IXK_HASORPOT and IXKN based on exogenously given or potential investment  ----------------------------------



*-------------------------------------------------------------------------------
* INITIALISATIONS FOR addons:
*-------------------------------------------------------------------------------
*-------------------------------------------------------------------------------

*----COMBTECH----

$ifi %COMBTECH%==yes IAGK_HASORPOT(Y,IA,IGCOMB2)$SUM(IGCOMB1$(IAGK_HASORPOT(Y,IA,IGCOMB1) and GGCOMB(IGCOMB1,IGCOMB2)),1)=yes;

*---END OF COMBTECH


*----- End of initialisation for the addons  -----------------------------------
*-------------------------------------------------------------------------------

*-------------------------------------------------------------------------------
*----- End of initialisation for annual updating part --------------------------
*-------------------------------------------------------------------------------

* Calculate ISIMWEIGHT for each simyear: Presently ISIMWEIGHT not really in use except here initially.
* ISIMWEIGHT is relevant in case that not all elements in an interval in Y are represented in IY411 (if YMODELDELTA if is larger than 1).
* It this case it might be assumed that the years not represented have the same values as the nearby years tht are elements.
* Hence, by giving the represented years appropriately higher weights a more reasonable balance migth be achieved.
* Presently ISIMWEIGHT not really in use except here initially, and perhaps the thinking as inappropriate.

ISIMWEIGHT(YMODEL) = SUM(IYMODEL4$((1 LT ORD(IYMODEL4)) AND (ORD(IYMODEL4) <= ORD(YMODEL))), YMODELDELTA(IYMODEL4));

*-------------------------------------------------------------------------------
* Initialize total capacity variables to start values.
*-------------------------------------------------------------------------------








*------------------------------------------------------------------------------------------------------------------------------------------------------------
*------------------------------------------------------------------------------------------------------------------------------------------------------------
*------------------------------------------------------------------------------------------------------------------------------------------------------------
* LOOP OVER ALL THE FIRSTYEARS IN ALL MODELS BALBASE4:
*------------------------------------------------------------------------------------------------------------------------------------------------------------
*------------------------------------------------------------------------------------------------------------------------------------------------------------

*LOOP(IYALIAS$(((CARD(IYALIAS)-ORD(IYALIAS) GE (SUM(YMODEL$(ORD(YMODEL) GT 1), YMODELDELTA(YMODEL)))) AND (YVALUE(IYALIAS) GE IFIRSTYEARNEXTBB4)) ), Hans 20150516: replace by next. If OK, IFIRSTYEARNEXTBB4 may be deleted, it is then not in use.    todo
LOOP(IYALIAS$(CARD(IYALIAS)-ORD(IYALIAS) GE (SUM(YMODEL$(ORD(YMODEL) GT 1), YMODELDELTA(YMODEL)))),


*------------------------------------------------------------------------------
* INITIALISATIONS FOR THE UPDATING PART FOR EACH MODEL BALBASE4:
*------------------------------------------------------------------------------


* -----------------------------------------------------------------------------
* Calculate IORD_IY, and subsets of Y:  TODO: IORD_IY is used only here for assigning the IY4's. Might therefore be eliminated by making some other code.
* IY411: the years in the present Balbase4 model
* IY410: IY411 except last year.
* IY401: IY411 except first year.
* IY411FIRST(IYALIAS): first year in IY411.
* IY4PREVY(IYALIAS): previous year in IY411.
* -----------------------------------------------------------------------------


ISCALAR1 = 0;
LOOP(YMODEL,
   ISCALAR1 = ISCALAR1 + YMODELDELTA(YMODEL)$(ORD(YMODEL) GT 1);
   LOOP(IYALIAS44$(ORD(IYALIAS44) EQ (ORD(IYALIAS)+ISCALAR1)),
   IORD_IY(YMODEL) = ORD(IYALIAS44);
));


IY411(Y) = NO;
LOOP((YMODEL, Y)$(IORD_IY(YMODEL) = ORD(Y)),IY411(Y)=yes;);
IY410(Y) = NO;
LOOP((YMODEL, Y)$((IORD_IY(YMODEL) = ORD(Y)) AND (ORD(YMODEL) < CARD(YMODEL))),IY410(Y)=yes;);
IY401(Y) = NO;
LOOP((YMODEL, Y)$((IORD_IY(YMODEL) = ORD(Y)) AND (ORD(YMODEL) > 1)),IY401(Y)=yes;);
IY411FIRST(Y)= NO;
LOOP(IY411$(CARD(IY411FIRST) = 0),  IY411FIRST(IY411)=yes;);

ISCALAR1 = 1;
LOOP(IY411$(ISCALAR1 EQ 1), ISCALAR2 = YVALUE(IY411); ISCALAR1 = 0;);
*LOOP(IY402$(YVALUE(IY402) EQ ISCALAR2), IFIRSTYEARNEXTBB4 = ISCALAR2 + SUM(YMODEL$(ORD(YMODEL) EQ 1), YMODELDELTA(YMODEL)););   todo


$ontext
* IY4GREMAINYWEIGHT(Y,G)  "Discounted weight of future annuity payments on an investment relative to the first year in present BB4 model (~)":
* TODO: missing that some G will reach lifetime inside iy411, cf. IY4GREMAINY above, TODO
IY4GINVCOSTWEIGHT(IY411,IGKFIND) = SUM(Y$(IY411(Y) AND (YVALUE(Y) GE YVALUE(IY411))), IDISCOUNTFACTOR(Y));!! Hans 20150205; TODO: missing that some G will reach lifetime inside iy411, cf. IY4GREMAINY above, TODO
LOOP(IY411FIRST,IY4GINVCOSTWEIGHT(IY411,IGKFIND) = IY4GINVCOSTWEIGHT(IY411,IGKFIND)/IDISCOUNTFACTOR(IY411FIRST));  !! NB: Relative to IY411FIRST, not first Y.

IY4GINVCOSTWEIGHT(Y,IGKFIND)$(NOT(IY411(Y))) = 0;

IY4XINVCOSTWEIGHT(IY411) = SUM(Y$(IY411(Y) AND (YVALUE(Y) GE YVALUE(IY411))), IDISCOUNTFACTOR(Y));
LOOP(IY411FIRST,IY4XINVCOSTWEIGHT(IY411) = IY4XINVCOSTWEIGHT(IY411)/IDISCOUNTFACTOR(IY411FIRST));  !! NB: Relative to IY411FIRST, not first Y.

LOOP(IY411FIRST,IY4GINVCOSTWEIGHT(IY411,IGKFIND) = 1);
LOOP(IY411FIRST,IY4XINVCOSTWEIGHT(IY411) = 1);
$offtext


*-------------------------------------------------------------------------------
* START SIMULATION FOR CURRENT MODEL BB4
*-------------------------------------------------------------------------------


* ------------------------------------------------------------------------------
* Update parameters and bounds for current simulation year:
* ------------------------------------------------------------------------------

*-------------------------------------------------------------------------------
* Set bounds on variables current simulation year
*-------------------------------------------------------------------------------

*---- Bounds on generation technologies: ---------------------------------------

* Generation on existing plants (except inter and intra seasonal storages) is restricted by the capacity (MW) adjusted by GKDERATE:
* (For storages and fluctuating sources (wind, etc.) overwritten later.)

* Complements to equations QGKE_UP and QGKH_UP for the case with no endogenous IGKE and IGKH investments.
VGE_T.UP(IY411,IA,IGDISPATCH(IGKE),S,T)$(GKFX(IY411,IA,IGKE) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGKE))) AND (NOT IGESTOALL(IGKE))) = GKFX(IY411,IA,IGKE)*IGKRATE(IA,IGKE,S,T);
VGH_T.UP(IY411,IA,IGDISPATCH(IGKH),S,T)$(GKFX(IY411,IA,IGKH) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGKH))) AND (NOT IGHSTOALL(IGKH))) = GKFX(IY411,IA,IGKH)*IGKRATE(IA,IGKH,S,T);

$ifi %testbb1234%==yes  IBB1234_VGE_T.UP(IY411,IA,IGDISPATCH(IGKE),S,T)     = VGE_T.UP(IY411,ia,IGKE,s,t);
$ifi %testbb1234%==yes  IBB1234_VGH_T.UP(IY411,IA,IGDISPATCH(IGKE),S,T)     = VGH_T.UP(IY411,ia,IGKE,s,t);
$ifi %testbb1234%==yes  execute_unload "..\..\simex\VGx_up_bb4_1.gdx" IBB1234_VGE_T , IBB1234_VGH_T ;

* Storage loading (MW) and unloading (MW) do not exceed capacity.
* Complements to equations QESTOLOADTLIM and QHSTOLOADTLIM for the case with no endogenous IGESTO and IGHSTO investments.

*Unloading
VGE_T.UP(IY411,IA,IGESTO,S,T)$(GKFX(IY411,IA,IGESTO) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGESTO)))) = GKFX(IY411,IA,IGESTO)*IGKRATE(IA,IGESTO,S,T)/GDATA(IGESTO,'GDSTOHUNLD');
VGH_T.UP(IY411,IA,IGHSTO,S,T)$(GKFX(IY411,IA,IGHSTO) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGHSTO)))) = GKFX(IY411,IA,IGHSTO)*IGKRATE(IA,IGHSTO,S,T)/GDATA(IGHSTO,'GDSTOHUNLD');
VGE_T.UP(IY411,IA,IGESTOS,S,T)$(GKFX(IY411,IA,IGESTOS) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGESTOS)))) = GKFX(IY411,IA,IGESTOS)*IGKRATE(IA,IGESTOS,S,T)/GDATA(IGESTOS,'GDSTOHUNLD');
VGH_T.UP(IY411,IA,IGHSTOS,S,T)$(GKFX(IY411,IA,IGHSTOS) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGHSTOS)))) = GKFX(IY411,IA,IGHSTOS)*IGKRATE(IA,IGHSTOS,S,T)/GDATA(IGHSTOS,'GDSTOHUNLD');

*Loading
VESTOLOADT.UP(IY411,IA,IGESTO,S,T)$(GKFX(IY411,IA,IGESTO) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGESTO)))) =
  IGKRATE(IA,IGESTO,S,T)*GKFX(IY411,IA,IGESTO)/GDATA(IGESTO,'GDSTOHLOAD');
VHSTOLOADT.UP(IY411,IA,IGHSTO,S,T)$(GKFX(IY411,IA,IGHSTO) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGHSTO)))) =
  IGKRATE(IA,IGHSTO,S,T)*GKFX(IY411,IA,IGHSTO)/GDATA(IGHSTO,'GDSTOHLOAD');
VESTOLOADTS.UP(IY411,IA,IGESTOS,S,T)$(GKFX(IY411,IA,IGESTOS) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGESTOS)))) =
  IGKRATE(IA,IGESTOS,S,T)*GKFX(IY411,IA,IGESTOS)/GDATA(IGESTOS,'GDSTOHLOAD');
VHSTOLOADTS.UP(IY411,IA,IGHSTOS,S,T)$(GKFX(IY411,IA,IGHSTOS) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGHSTOS)))) =
  IGKRATE(IA,IGHSTOS,S,T)*GKFX(IY411,IA,IGHSTOS)/GDATA(IGHSTOS,'GDSTOHLOAD');

* Storage content does not exceed upper limit (MWh).
* Complements to equations QESTOLOADTLIM and QHSTOLOADTLIM for the case with no endogenous IGESTO and IGHSTO investments.

VESTOVOLT.UP(IY411,IA,IGESTO,S,T)$(GKFX(IY411,IA,IGESTO) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGESTO)))) =
  GKFX(IY411,IA,IGESTO);
VHSTOVOLT.UP(IY411,IA,IGHSTO,S,T)$(GKFX(IY411,IA,IGHSTO) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGHSTO)))) =
  GKFX(IY411,IA,IGHSTO);

VESTOVOLTS.UP(IY411,IA,IGESTOS,S,T)$(GKFX(IY411,IA,IGESTOS) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGESTOS)))) =
 GKFX(IY411,IA,IGESTOS);
VHSTOVOLTS.UP(IY411,IA,IGHSTOS,S,T)$(GKFX(IY411,IA,IGHSTOS) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGHSTOS)))) =
 GKFX(IY411,IA,IGHSTOS);


$ifi %testbb1234%==yes  IBB1234_VGE_T.UP(IY411,IA,G,S,T)     = VGE_T.UP(IY411,ia,g,s,t);
$ifi %testbb1234%==yes  IBB1234_VGH_T.UP(IY411,IA,G,S,T)     = VGH_T.UP(IY411,ia,g,s,t);
$ifi %testbb1234%==yes  execute_unload "..\..\simex\VGx_up_bb4_2.gdx" IBB1234_VGE_T , IBB1234_VGH_T ;

$ifi %testbb1234%==yes  IBB1234_VGE_T.UP(IY411,IA,G,S,T)     = VGE_T.UP(IY411,ia,g,s,t);
$ifi %testbb1234%==yes  IBB1234_VGH_T.UP(IY411,IA,G,S,T)     = VGH_T.UP(IY411,ia,g,s,t);
$ifi %testbb1234%==yes  execute_unload "..\..\simex\VGx_up_bb4_3.gdx" IBB1234_VGE_T , IBB1234_VGH_T ;
* test slut

* Existing windpower, hydro-run-of-river, solarpower, solarheat and wavepower can not be dispatched.
* Complement to equation QWNDDISPATCH for the case with no endogenous IGWND investments.
$ontext
$ifi not %VRESHUTDOWN%==yes VGE_T.FX(IY411,IA,IGWND,S,T)$(GKFX(IY411,IA,IGWND) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGWND)))
$ifi %VRESHUTDOWN%==yes     VGE_T.UP(IY411,IA,IGWND,S,T)$(GKFX(IY411,IA,IGWND) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGWND)))
$ifi %STEPWISEKPOT%==YES            AND NOT IKPOTSTEP(IA,IGWND)
$ifi %WNDFLH_DOL%==AAA        ) = ((WNDFLH(IA)       * GKFX(IY411,IA,IGWND) * IGKRATE(IA,IGWND,S,T) * WND_VAR_T(IA,S,T)) / IWND_SUMST(IA))$IWND_SUMST(IA);
$ifi %WNDFLH_DOL%==AAA_GGG    ) = ((WNDFLH(IA,IGWND) * GKFX(IY411,IA,IGWND) * IGKRATE(IA,IGWND,S,T) * WND_VAR_T(IA,S,T)) / IWND_SUMST(IA))$IWND_SUMST(IA)
$offtext


$ifi %TechInvest%==yes $goto NotTechInvestWindgen
$ifi not %STEPWISEKPOT%==YES      $goto Nostepwisepot
$ifi not %VRESHUTDOWN%==yes VGE_T.FX(IY411,IA,IGWND,S,T)$(GKFX(IY411,IA,IGWND) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGWND))) AND IWND_SUMST(IA) AND IKPOTSTEP(IA,IGWND)
$ifi %VRESHUTDOWN%==yes     VGE_T.UP(IY411,IA,IGWND,S,T)$(GKFX(IY411,IA,IGWND) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGWND))) AND IWND_SUMST(IA) AND IKPOTSTEP(IA,IGWND)
$ifi %WNDFLH_DOL%==AAA             ) = WNDFLH(IA)               * IGKRATE(IA,IGWND,S,T) * WND_VAR_T(IA,S,T) * SUM(KPOTSTEP,  IGKFX_KPOTSTEP(IY411,IA,IGWND,KPOTSTEP) *IKPOTSTEP_FLHA(IA,IGWND,KPOTSTEP) ) / IWND_SUMST(IA);
$ifi %WNDFLH_DOL%==AAA_GGG  ) = WNDFLH(IA,IGWND) * IGKRATE(IA,IGWND,S,T) * WND_VAR_T(IA,S,T) * SUM(KPOTSTEP,  IGKFX_KPOTSTEP(IY411,IA,IGWND,KPOTSTEP) *IKPOTSTEP_FLHA(IA,IGWND,KPOTSTEP) ) / IWND_SUMST(IA);

$ifi not %VRESHUTDOWN%==yes VGE_T.FX(IY411,IA,IGWND,S,T)$(GKFX(IY411,IA,IGWND) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGWND))) AND IWND_SUMST(IA) AND NOT IKPOTSTEP(IA,IGWND)) =
$ifi %VRESHUTDOWN%==yes     VGE_T.UP(IY411,IA,IGWND,S,T)$(GKFX(IY411,IA,IGWND) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGWND))) AND IWND_SUMST(IA) AND NOT IKPOTSTEP(IA,IGWND)) =
$ifi %WNDFLH_DOL%==AAA        ((WNDFLH(IA)       * GKFX(IY411,IA,IGWND) * IGKRATE(IA,IGWND,S,T) * WND_VAR_T(IA,S,T)) / IWND_SUMST(IA))$IWND_SUMST(IA);
$ifi %WNDFLH_DOL%==AAA_GGG    ((WNDFLH(IA,IGWND) * GKFX(IY411,IA,IGWND) * IGKRATE(IA,IGWND,S,T) * WND_VAR_T(IA,S,T)) / IWND_SUMST(IA))$IWND_SUMST(IA);
$label Nostepwisepot

$ifi %STEPWISEKPOT%==YES      $goto yesstepwisepot
$ifi not %VRESHUTDOWN%==yes VGE_T.FX(IY411,IA,IGWND,S,T)$(GKFX(IY411,IA,IGWND) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGWND))) AND IWND_SUMST(IA)  =
$ifi %VRESHUTDOWN%==yes     VGE_T.UP(IY411,IA,IGWND,S,T)$(GKFX(IY411,IA,IGWND) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGWND))) AND IWND_SUMST(IA)  =
$ifi %WNDFLH_DOL%==AAA        ((WNDFLH(IA)       * GKFX(IY411,IA,IGWND) * IGKRATE(IA,IGWND,S,T) * WND_VAR_T(IA,S,T)) / IWND_SUMST(IA))$IWND_SUMST(IA);
$ifi %WNDFLH_DOL%==AAA_GGG    ((WNDFLH(IA,IGWND) * GKFX(IY411,IA,IGWND) * IGKRATE(IA,IGWND,S,T) * WND_VAR_T(IA,S,T)) / IWND_SUMST(IA))$IWND_SUMST(IA);
$label yesstepwisepot

$label NotTechInvestWindgen

$ifi not %TechInvest%==yes $goto TechInvestWindgen
$ifi not %VRESHUTDOWN%==yes VGE_T.FX(IY411,IA,IGWND,S,T)$(GKFX(IY411,IA,IGWND) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGWND)))) =
$ifi %VRESHUTDOWN%==yes     VGE_T.UP(IY411,IA,IGWND,S,T)$(GKFX(IY411,IA,IGWND) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGWND))) ) =
$ifi %WNDFLH_DOL%==AAA        ((WNDFLH(IA)       * GKFX(IY411,IA,IGWND) * IGKRATE(IA,IGWND,S,T) * WND_VAR_T(IA,S,T)) / IWND_SUMST(IA))$IWND_SUMST(IA);
$ifi %WNDFLH_DOL%==AAA_GGG    ((WNDFLH(IA,IGWND) * GKFX(IY411,IA,IGWND) * IGKRATE(IA,IGWND,S,T) * WND_VAR_T(IA,S,T)) / IWND_SUMST(IA))$IWND_SUMST(IA);
$label TechInvestWindgen

* Complement to equation QHYRRDISPATCH for the case with no endogenous IGHYRR investments.
$ifi not %VRESHUTDOWN%==yes VGE_T.FX(IY411,IA,IGHYRR,S,T)$(GKFX(IY411,IA,IGHYRR) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGHYRR)))) =
$ifi %VRESHUTDOWN%==yes     VGE_T.UP(IY411,IA,IGHYRR,S,T)$(GKFX(IY411,IA,IGHYRR) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGHYRR)))) =
   ((WTRRRFLH(IA)* GKFX(IY411,IA,IGHYRR) * IGKRATE(IA,IGHYRR,S,T) * WTRRRVAR_T(IA,S,T)) / IWTRRRSUM(IA))$IWTRRRSUM(IA);

* Complement to equation QSOLEDISPATCH for the case with no endogenous IGSOLE investments.
$ifi not %VRESHUTDOWN%==yes VGE_T.FX(IY411,IA,IGSOLE,S,T)$(GKFX(IY411,IA,IGSOLE) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGSOLE)))) =
$ifi %VRESHUTDOWN%==yes     VGE_T.UP(IY411,IA,IGSOLE,S,T)$(GKFX(IY411,IA,IGSOLE) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGSOLE)))) =
$ifi %SOLEFLH_DOL%==AAA       ((SOLEFLH(IA)        * GKFX(IY411,IA,IGSOLE) * IGKRATE(IA,IGSOLE,S,T) * SOLE_VAR_T(IA,S,T)) / ISOLESUMST(IA))$ISOLESUMST(IA);
$ifi %SOLEFLH_DOL%==AAA_GGG   ((SOLEFLH(IA,IGSOLE) * GKFX(IY411,IA,IGSOLE) * IGKRATE(IA,IGSOLE,S,T) * SOLE_VAR_T(IA,S,T)) / ISOLESUMST(IA))$ISOLESUMST(IA);

* Complement to equation QSOLHDISPATCH for the case with no endogenous IGSOLH investments.
$ifi not %VRESHUTDOWN%==yes VGH_T.FX(IY411,IA,IGSOLH,S,T)$(GKFX(IY411,IA,IGSOLH) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGSOLH)))) =
$ifi %VRESHUTDOWN%==yes     VGH_T.UP(IY411,IA,IGSOLH,S,T)$(GKFX(IY411,IA,IGSOLH) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGSOLH)))) =
$ifi %SOLHFLH_DOL%==AAA       ((SOLHFLH(IA) * GKFX(IY411,IA,IGSOLH) * IGKRATE(IA,IGSOLH,S,T) * SOLH_VAR_T(IA,S,T)) / ISOLHSUMST(IA))$ISOLHSUMST(IA);
$ifi %SOLHFLH_DOL%==AAA_GGG   ((SOLHFLH(IA,IGSOLH) * GKFX(IY411,IA,IGSOLH) * IGKRATE(IA,IGSOLH,S,T) * SOLH_VAR_T(IA,S,T)) / ISOLHSUMST(IA))$ISOLHSUMST(IA);

* Complement to equation QWAVEDISPATCH for the case with no endogenous IGWAVE investments.
$ifi not %VRESHUTDOWN%==yes VGE_T.FX(IY411,IA,IGWAVE,S,T)$(GKFX(IY411,IA,IGWAVE) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGWAVE)))) =
$ifi %VRESHUTDOWN%==yes     VGE_T.UP(IY411,IA,IGWAVE,S,T)$(GKFX(IY411,IA,IGWAVE) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGWAVE)))) =
$ifi %GWAVE_DOL%==AAA   ((WAVEFLH(IA) * GKFX(IY411,IA,IGWAVE) * IGKRATE(IA,IGWAVE,S,T) * WAVE_VAR_T(IA,S,T)) / IWAVESUMST(IA))$IWAVESUMST(IA);
$ifi %GWAVE_DOL%==AAA_GGG   ((WAVEFLH(IA,IGWAVE) * GKFX(IY411,IA,IGWAVE) * IGKRATE(IA,IGWAVE,S,T) * WAVE_VAR_T(IA,S,T)) / IWAVESUMST(IA))$IWAVESUMST(IA);

$ifi %testbb1234%==yes  IBB1234_VGE_T.UP(IY411,IA,G,S,T)     = VGE_T.UP(IY411,ia,g,s,t);
$ifi %testbb1234%==yes  IBB1234_VGH_T.UP(IY411,IA,G,S,T)     = VGH_T.UP(IY411,ia,g,s,t);
$ifi %testbb1234%==yes  execute_unload "..\..\simex\VGx_up_bb4_5.gdx" IBB1234_VGE_T , IBB1234_VGH_T ;

* Calculate hydro inflow to reservoirs in MWh/MW installed capacity (new and old):

IHYINF_S(IA,S)= ((WTRRSFLH(IA) * WTRRSVAR_S(IA,S) * (WEIGHT_S(S)/IWEIGHSUMS)) / IWTRRSSUM(IA))$IWTRRSSUM(IA);

*Flex4RES
* quickfix for nuclear flexibility
$ifi %GKRATE_DOL%==AAA_GGG_SSS     VGE_T.lo(IY411,IA,G,S,T)$IGF(G,'NUCLEAR')=GKFX(IY411,IA,G)*(1$(NOT IGKRATE(IA,G,S,T)) + IGKRATE(IA,G,S,T))*0.7;
$ifi %GKRATE_DOL%==AAA_GGG_SSS_TTT VGE_T.lo(IY411,IA,G,S,T)$IGF(G,'NUCLEAR')=GKFX(IY411,IA,G)*(1$(NOT IGKRATE(IA,G,S,T)) + IGKRATE(IA,G,S,T))*0.7;

* Generation on existing plants (except inter and intra seasonal storages) is restricted by the capacity (MW) adjusted by GKDERATE:
* (For storages and fluctuating sources (wind, etc.) overwritten later.)
*F4R


*---- End: Bounds on generation technologies -----------------------------------

*---- Bounds on transmission: --------------------------------------------------

* Update bounds:
* Complement to equation QXK_UP for the case with no endogenous transmission investments.
VX_T.UP(IY411,IRE,IRI,S,T)$(IXK_HASORPOT(IY411,IRE,IRI) AND (NOT SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IXKN(Y,IRE,IRI)))) = XKFX(IY411,IRE,IRI)*IXKRATE(IRE,IRI,S,T);


* Set the fixed exchange with third regions:

IX3FX_T(IY411,IR,S,T) = ((X3FX(IY411,IR) * X3FX_VAR_T(IR,S,T)) / IX3FXSUMST(IR))$(IX3FXSUMST(IR) NE 0);


* Electricity transmission between regions within countries and between
* countries may be excluded by the following statement:

* VX_T.FX(IY411,IRE,IRI,S,T)=0;

* Electricity transmission between countries may be excluded
* by the following statement:
* LOOP(C, VX_T.FX(IY411,IRE,IRI,S,T)$(NOT(CCCRRR(C,IRE) AND CCCRRR(C,IRI)))=0);


*---- End Bounds on transmission -----------------------------------------------


*-------------------------------------------------------------------------------
*---- Annual updating wrt. options and add-on parameters: ----------------------
*-------------------------------------------------------------------------------

$include "../../base/addons/_hooks/sim.inc"
$include "../../base/addons/_hooks/varloupfx.inc"

IAGK_NOINVEST(IY411,IA,G)$IYFIRST(IY411) = 0;
IAGK_HASORPOT(IY411,IA,G)$(SUM(IY402$IY411PREVY(IY411,IY402), IAGK_NOINVEST(IY402,IA,G))) = NO;

*-------------------------------------------------------------------------------
*---- End: Annual updating wrt. options and add-on parameters: -----------------
*-------------------------------------------------------------------------------

*-------------------------------------------------------------------------------
* End: Set bounds on variables current simulation year
*-------------------------------------------------------------------------------

*-------------------------------------------------------------------------------
* SOLVE
*-------------------------------------------------------------------------------

* The Savepoint option must be active especially for BB1 and BB2 since this outputs the     REVISE
* solution basis, which is used for generating results database.
OPTION Savepoint=1;

$ifi not %USEOPTIONFILE%==0 BALBASE4.optfile = %USEOPTIONFILE%;


$ifi not %SOLVEMIP%==yes SOLVE BALBASE4 USING LP  MINIMIZING VOBJ;  !! =====================
$ifi  %SOLVEMIP%==yes SOLVE BALBASE4 USING MIP MINIMIZING VOBJ;  !! =====================

$include "../../base/addons/_hooks/solveend.inc"
*-------------------------------------------------------------------------------
* End: SOLVE
*-------------------------------------------------------------------------------

$ifi %bb4%==yes $INCLUDE '../../base/logerror/logerinc/balbase4.mss';


* ------------------------------------------------------------------------------
* --- Verify and Save results --------------------------------------------------
* ------------------------------------------------------------------------------


*-------------------------------------------------------------------------------
*-------------------------------------------------------------------------------
$ifi not %system.filesys%==MSNT $goto endofMSNTmove

$ifi %MERGESAVEPOINTRESULTS%==yes
$ifi %bb4%==yes  putclose  fileMERGESAVEPOINTRESULTSbat 'move "%relpathModel%''BALBASE4_p.gdx" '  '"%relpathModel%%relpathoutput%temp/'IYALIAS.tl:0:0 '.gdx"';
$ifi %MERGESAVEPOINTRESULTS%==yes
$ifi %bb4%==yes  execute  "%batfileMERGESAVEPOINTRESULTS%";

$label endofMSNTmove

$ifi not %system.filesys%==UNIX $goto endofUNIXmove

$ifi %MERGESAVEPOINTRESULTS%==yes
$ifi %bb4%==yes  putclose  fileMERGESAVEPOINTRESULTSbash '#!/bin/bash' / 'mv "%relpathModel%''BALBASE4_p.gdx" "%relpathModel%%relpathoutput%temp/'IYALIAS.tl:0:0 '.gdx"';

$ifi %MERGESAVEPOINTRESULTS%==yes
$ifi %bb4%==yes execute 'chmod +x ./"%bashfileMERGESAVEPOINTRESULTS%"';

$ifi %MERGESAVEPOINTRESULTS%==yes
$ifi %bb4%==yes  execute  "./%bashfileMERGESAVEPOINTRESULTS%";

$label endofUNIXmove
*-------------------------------------------------------------------------------
*-------------------------------------------------------------------------------

* Certain checks may be made on the output:
*$INCLUDE '../../base/logerror/logerinc/error4-BB4.inc';
$INCLUDE '../../base/addons/BB4/errorBB4.inc';


*..............................................................................


* Case specific output printing:
$ifi EXIST 'caseprint.inc' $include 'caseprint.inc';

$ifi %printfiles%==yes $if errorfree $include "../../base/output/printout/printinc/prtbb4.inc";

* ------------------------------------------------------------------------------
* --- End: Verify and Save results ---------------------------------------------
* ------------------------------------------------------------------------------


* ------------------------------------------------------------------------------
* --- Prepare the next BB4 model -----------------------------------------------
* ------------------------------------------------------------------------------

*Fixing the investments and accumulated investments from previous optimization for the next optimization (only the first year of IY411 is fixed)
*(IMPORTANT!: The following code only works if the years are optimized consecutively. For example, this two-year mode optimization would not provide coherent results: [2020,2040] and [2030;2050]
$ifi %TechInvest%==yes         LOOP(Y$(YVALUE(Y) LE YVALUE(IYALIAS)),VGKN.FX(Y,IA,G) = VGKN.L(Y,IA,G););
$ifi %TechInvest%==yes         VGKNACCUMNET.FX(IY411FIRST,IA,G)$SUM(Y$(YVALUE(Y) LE YVALUE(IY411FIRST)),IAGKNY(Y,IA,G)) = VGKNACCUMNET.L(IY411FIRST,IA,G);
*$ifi %TechInvest%==yes        VGKNACCUMGROSS.FX(IY411FIRST,IA,G)$SUM(Y$(YVALUE(Y) LE YVALUE(IY411FIRST)),IAGKNY(Y,IA,G)) = VGKNACCUMGROSS.L(IY411FIRST,IA,G);
$ifi %TransInvest%==yes        VXKNACCUMNET.FX(IY411FIRST,IRE,IRI)$SUM(Y$(YVALUE(Y) LE YVALUE(IY411FIRST)),IXKN(Y,IRE,IRI))= VXKNACCUMNET.L(IY411FIRST,IRE,IRI);
$ifi %TechInvest%==yes         IAGK_NOINVEST(Y,IA,G)$(IY411FIRST(Y) AND IAGKN(IA,G) AND (YVALUE(Y+1) GT GDATA(G,'GDLASTYEAR')) AND NOT VGKNACCUMNET.L(Y,IA,G)) = yes;
$ifi %TransInvest%==yes        VXKN.FX(IY411FIRST,IRE,IRI)$SUM(Y$(YVALUE(Y) LE YVALUE(IY411FIRST)),IXKN(Y,IRE,IRI))=  VXKN.L(IY411FIRST,IRE,IRI);



$ifi %DemandResponse%==yes     VDRKN_ACCUMNET.FX(IY411FIRST,IA,DR_TECH)$SUM(Y$(YVALUE(Y) LE YVALUE(IY411FIRST)),IADR(Y,IA,DR_TECH))= VDRKN_ACCUMNET.L(IY411FIRST,IA,DR_TECH);
$include "../../base/addons/_hooks/varassign.inc"

* ------------------------------------------------------------------------------
* --- End: Prepare the next BB4 model ------------------------------------------
* ------------------------------------------------------------------------------


*------------------------------------------------------------------------------
*                    END OF SIMULATION FOR CURRENT BB4 MODEL
*------------------------------------------------------------------------------
); !! LOOP(IYALIAS,...)
*------------------------------------------------------------------------------
*-------------------------------------------------------------------------------
* END: LOOP OVER ALL THE FIRSTYEARS IN ALL MODELS BALBASE4
*-------------------------------------------------------------------------------


*------------------------------------------------------------------------------
*                GENERATE INPUT FILES FOR BB3 FROM RESULTS OF BB4
*------------------------------------------------------------------------------

* For hydro with reservoir save the following, to be used in model BalBase3 by addon hyrsbb123:
* Preliminary code.
$ifi %bb4%==yes  HYRSG(Y,IA,S)$SUM(IGHYRS,IAGK_HASORPOT(Y,IA,IGHYRS)) = SUM(IGHYRS$IAGK_HASORPOT(Y,IA,IGHYRS),SUM(T, IHOURSINST(S,T)*VGE_T.L(Y,IA,IGHYRS,S,T)));
$ifi %bb4%==yes  VHYRS_SL(Y,IA,S)  = VHYRS_S.l(Y,IA,S);
$ifi %bb4%==yes  WATERVAL(Y,IA,S)  = QHYRSSEQ.m(Y,IA,S);

*------------------------------------------------------------------------------
*                GENERATE INVESTMENT DATA FILES
*------------------------------------------------------------------------------

$ifi %BB4%==yes
$ifi %TransInvest%==yes XKACC(Y,IRE,IRI) = VXKNACCUMNET.L(Y,IRE,IRI);
$ifi %BB4%==yes
$ifi %TechInvest%==yes GKVACCDECOM(Y,IA,IGKFIND) = VGKNACCUMNET.L(Y,IA,IGKFIND);

$ifi %SaveInvestData%==yes execute_unload '../../simex/GENINVEST_%CASEID%.gdx', GKVACCDECOM,VGKNACCUMNET_KPOTSTEP;
$ifi %SaveInvestData%==yes execute_unload '../../simex/TRANSINVEST_%CASEID%.gdx', XKACC;


*-------------------------------------------------------------------------------
* End of file
*-------------------------------------------------------------------------------
