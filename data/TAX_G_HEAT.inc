***                                                                                                                                                                                                                      Taxes on heat production                                                                                              
***                                                                                                                                                                                                                      Negative values are interpreted as subsidies                                                                          
***                                                                                                                                                                                                                      Oct-2018. Jon Gustav Kirkerud                                                                                         
PARAMETER TAX_G_HEAT(YYY,CCC,GGG) 'tax on heat generation';                                                                                                                                                                                                                                                                                    
                                                                                                                                                                                                                                                                                                                                               
*** Main input format: per country and per fuel                                                                                                                                                                                                                                                                                                
TABLE TAX_G_HEAT_IN(YYY,CCC,FFF) 'tax on heat generation'                                                                                                                                                                                                                                                                                      
                                                                                                                                                                                                                         NUCLEAR                                      NATGAS COAL  LIGNITE FUELOIL          LIGHTOIL SHALE PEAT  LNG   ELECTRIC
2015.FINLAND                                                                                                                                                                                                                                                          11.09  15.74         13.4958117982534 13.7           11.09               
2018.FINLAND                                                                                                                                                                                                                                                          13.72  18.59         16.96            16.25          13.72               
2015.DENMARK                                                                                                                                                                                                                                                          32.15  34.08         32.9             32.9                 32.15         
2015.SWEDEN                                                                                                                                                                                                                                                           5      7.03          7.86             7.86           5                   
;                                                                                                                                                                                                                                                                                                                                              
                                                                                                                                                                                                                                                                                                                                               
* Convert from €2015 to €2012                                                                                                                                                                                                                                                                                                                  
TAX_G_HEAT_IN(YYY,CCC,FFF) = TAX_G_HEAT_IN(YYY,CCC,FFF) * 0.9799;                                                                                                                                                                                                                                                                              
                                                                                                                                                                                                                                                                                                                                               
* Apply 2015 numbers to all years:                                                                                                                                                                                                                                                                                                             
TAX_G_HEAT_IN(Y,C,FFF)$(YVALUE(Y) NE 2015) = TAX_G_HEAT_IN('2015',C,FFF);                                                                                                                                                                                                                                                                      
                                                                                                                                                                                                                                                                                                                                               
* Assign values onto TAX_G_HEAT                                                                                                                                                                                                                                                                                                                
TAX_G_HEAT(Y,C,IGH)$((SUM(IA$ICA(C,IA), GKFX(Y,IA,IGH)) > 0) OR SUM(IA$ICA(C,IA), AGKN(IA,IGH)))  = SUM(FFF$IGF(IGH,FFF), TAX_G_HEAT_IN(Y,C,FFF));                                                                                                                                                                                             
                                                                                                                                                                                                                                                                                                                                               
                                                                                                                                                                                                                                                                                                                                               
*** Secondary input: exceptions from the main input on technology level                                                                                                                                                                                                                                                                        
                                                                                                                                                                                                                                                                                                                                               
* FINLAND:                                                                                                                                                                                                                                                                                                                                     
** for Finland, this tax only considers CHP. for all technologies except CHP, the tax is zero.                                                                                                                                                                                                                                                 
TAX_G_HEAT(Y,'FINLAND',G)$(NOT (IGBPR(G) OR IGEXT(G))) = 0;                                                                                                                                                                                                                                                                                    
* The tariff is charged onto heat generation x 0.9                                                                                                                                                                                                                                                                                             
TAX_G_HEAT(Y,'FINLAND',G)$(IGBPR(G) OR IGEXT(G)) = TAX_G_HEAT(Y,'FINLAND',G) * 0.9;                                                                                                                                                                                                                                                            
                                                                                                                                                                                                                                                                                                                                               
                                                                                                                                                                                                                                                                                                                                               
* DENMARK:                                                                                                                                                                                                                                                                                                                                     
** Alternative values for Danish plants because of E-rule and V-rule                                                                                                                                                                                                                                                                           
* it is assumed that extraction plants follow the V-rule, so for all other plants this tax is set to zero                                                                                                                                                                                                                                      
TAX_G_HEAT(Y,'DENMARK',G)$(NOT (IGBPR(G) OR IGEXT(G))) = 0;                                                                                                                                                                                                                                                                                    
* V-rule:                                                                                                                                                                                                                                                                                                                                      
TAX_G_HEAT(Y,'DENMARK',IGEXT) = TAX_G_HEAT(Y,'DENMARK',IGEXT) / 1.2;                                                                                                                                                                                                                                                                           
* for back pressure: use V-rule if that result in lower cost than E-rule. If not, set to zero and apply in TAX_G_EL instead.                                                                                                                                                                                                                   
TAX_G_HEAT(Y,'DENMARK',IGBPR)$(  ( 1 - (GDATA(IGBPR,'GDCB') * GDATA(IGBPR,'GDFE') / (1 + GDATA(IGBPR,'GDCB')) ) / 0.67) > (GDATA(IGBPR,'GDFE')/ ( 1 + GDATA(IGBPR,'GDCB'))/1.2))  = TAX_G_HEAT(Y,'DENMARK',IGBPR) / 1.2;                                                                                                                       
TAX_G_HEAT(Y,'DENMARK',IGBPR)$(  ( 1 - (GDATA(IGBPR,'GDCB') * GDATA(IGBPR,'GDFE') / (1 + GDATA(IGBPR,'GDCB')) ) / 0.67) < (GDATA(IGBPR,'GDFE')/ ( 1 + GDATA(IGBPR,'GDCB'))/1.2))  = 0;                                                                                                                                                         
                                                                                                                                                                                                                                                                                                                                               
                                                                                                                                                                                                                                                                                                                                               
* SWEDEN                                                                                                                                                                                                                                                                                                                                       
** for Sweden, this tax only considers CHP. for all technologies except CHP, the tax is zero.                                                                                                                                                                                                                                                  
TAX_G_HEAT(Y,'SWEDEN',G)$(NOT (IGBPR(G) OR IGEXT(G))) = 0;                                                                                                                                                                                                                                                                                     
                                                                                                                                                                                                                                                                                                                                               
** Fuel use for heat consumption in CHP is in Swedish tax rules calculated accotding to heat share in production output                                                                                                                                                                                                                        
** Therefore, we calculate tax from heat production, but adjust the tax accotding to total efficiency of the plant                                                                                                                                                                                                                             
** NB: this formula does currently not take into account GEFFRATE.                                                                                                                                                                                                                                                                             
TAX_G_HEAT(Y,'SWEDEN',G)$(IGBPR(G) OR IGEXT(G))  = TAX_G_HEAT(Y,'SWEDEN',G) * (GDATA(G,'GDCB') + GDATA(G,'GDCV')) / (GDATA(G,'GDFE') * (1 + GDATA(G,'GDCB')));                                                                                                                                                                                 
